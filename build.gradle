plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1' apply false
    id "com.osacky.doctor" version "0.12.0"
    id 'com.diffplug.spotless' version '8.1.0'
}

allprojects {
    group = 'com.seatwise'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            url = uri('https://packages.confluent.io/maven/')
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    spotless {
        java {
            googleJavaFormat('1.28.0')
            removeUnusedImports()
            formatAnnotations()
        }

        groovyGradle {
            target '*.gradle', '**/*.gradle'
            greclipse()
        }

        format 'yaml', {
            target '.github/**/*.yml', '*.yaml', '*.yml'
            prettier(['prettier': '3.7.2'])
        }

        format 'misc', {
            target '.gitattributes', '.gitignore', '**/*.properties'
            trimTrailingWhitespace()
            leadingTabsToSpaces(4)
            endWithNewline()
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        systemProperty 'user.timezone', 'UTC'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs += ['-parameters']
    }
}